content = {SOI ~ (declaration | step | assertion | order)+ ~ query ~ EOI}

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

ident = @{  ASCII_ALPHA ~ ( ASCII_ALPHANUMERIC | "_" | "-")* }
underscore = {"_"}


type_name = @{ident}
function = @{ident}
variable = @{underscore? ~ ident}
step_name = @{ident}
operation = @{ inner_symbols ~ ("&" | "|" | inner_symbols)*}
    inner_symbols = _{"="| "/"| "."| "<"| ">"| "^"| "%" | "#"| "*" | "!"}

declaration = { declare_type | declare_function }
    declare_type = {"type" ~ type_name ~ ";"?}
    declare_function = {"fun" ~ function ~ declare_function_args ~ 
                          ":" ~ type_name ~ ";"?}
       declare_function_args = {("(" ~ (type_name ~ ",")* ~ type_name? ~ ")")?}

typed_arguments = {("(" ~ (variable_binding ~ ",")* ~ variable_binding? ~ ")")?}
    variable_binding = {variable ~ ":" ~ type_name }

term = {if_then_else | find_such_that | application }
application = {(function | variable) ~ ("(" ~ (term ~ ",")* ~ term? ~ ")")?}
if_then_else = {"if"~ inner_cond_if ~ bracketed_term ~ "else" ~ bracketed_term}
    inner_cond_if = _{ cnf | "(" ~ cnf ~ ")"}
find_such_that = {"try" ~ "find" ~ 
                    typed_arguments ~
                    "such" ~ "that"  ~ "{" ~ cnf ~ "}" ~
                    "then" ~ bracketed_term ~
                    "else" ~ bracketed_term}
bracketed_term = _{"{" ~ term ~ "}"}

quantifier = {quantifier_op ~ typed_arguments ~ "{" ~ cnf ~ "}"}
    quantifier_op = _{exists | forall }
	forall = {"forall"}
    exists = {"exists"}

infix_term = { inner_infix_term | ("(" ~ inner_infix_term ~ ")")}
    inner_infix_term = _{term ~ operation ~ term}


cnf = {inner_cnf ~ ("&&" ~ inner_cnf)*}
    inner_cnf = _{disjunction | ("(" ~ disjunction ~ ")")}

disjunction = {inner_disjunction ~ ("||" ~ inner_disjunction)*}
    inner_disjunction = _{ bool | ( "(" ~ bool ~ ")")}
	bool = {infix_term | quantifier | term }
    
step = {"step" ~ step_name ~ typed_arguments ~
         "{" ~ cnf ~ "}" ~ 
         "{" ~ term ~ "}" ~ ";"?}

assertion = {"assert" ~ cnf ~ ";"?}
query = {"query" ~ cnf ~ ";"?}

order = {"order" ~ quantifier_op ~ typed_arguments ~ 
    (inner_order | "{" ~ inner_order ~ "}") ~ ";"?}
    inner_order = _{application ~ ordering_operation ~ application}
    ordering_operation = _{"<" | "<>" | ">" }